#include "common.cuh"
#include "fattn-common.cuh"

// Per-TU sink state — in fattn-vec.cuh so kernel and host code share the same TU copy.
// Only K sinks are used in the VEC kernel. V sinks were removed from the V accumulation
// loop because managed memory reads in the hot loop caused -3% short / -12% 32K regression.
// Uses __device__ + cudaMemcpyToSymbolAsync (stream-ordered, graph-capturable).
// Previous __managed__ approach crashed on SM86 (page faults during graph replay).
static __device__ const half * d_fattn_sink_K_buf = nullptr;
static __device__ int          d_fattn_sink_n     = 0;
static __device__ int64_t      d_fattn_sink_ne0   = 0;

static int ggml_cuda_fattn_vec_get_nthreads_host(const int cc) {
    return 128;
    GGML_UNUSED(cc);
}

static constexpr __device__ int ggml_cuda_fattn_vec_get_nthreads_device() {
    return 128;
}

template<ggml_type type_K, int D, int nthreads>
static constexpr __host__ __device__ vec_dot_KQ_t get_vec_dot_KQ_fattn() {
    if constexpr (type_K == GGML_TYPE_TURBO3_0) {
        return nullptr;
    } else {
        return get_vec_dot_KQ<type_K, D, nthreads>();
    }
}

// Currently llvm with the amdgcn target does not support unrolling loops
// that contain a break that can not be resolved at compile time.
#ifdef __clang__
#pragma clang diagnostic push
#pragma clang diagnostic ignored "-Wpass-failed"
#endif // __clang__
template<int D, int ncols, ggml_type type_K, ggml_type type_V, bool use_logit_softcap> // D == head size
__launch_bounds__(ggml_cuda_fattn_vec_get_nthreads_device(), 3) // 3 blocks/SM = 12 warps = 25% occupancy on SM120
static __global__ void flash_attn_ext_vec(
        const char * __restrict__ Q,
        const char * __restrict__ K,
        const char * __restrict__ V,
        const char * __restrict__ mask,
        const char * __restrict__ sinks,
        const int  * __restrict__ KV_max,
        float      * __restrict__ dst,
        float2     * __restrict__ dst_meta,
        const float scale,
        const float max_bias,
        const float m0,
        const float m1,
        const uint32_t n_head_log2,
        const float logit_softcap,
        const int32_t ne00, const uint3   ne01, const int32_t ne02, const int32_t ne03,
                            const int32_t nb01, const int32_t nb02, const int32_t nb03,
        const int32_t ne10, const int32_t ne11, const int32_t ne12, const int32_t ne13,
                            const int32_t nb11, const int32_t nb12, const int64_t nb13,
                            const int32_t nb21, const int32_t nb22, const int64_t nb23,
                            const int32_t ne31, const int32_t ne32, const int32_t ne33,
                            const int32_t nb31, const int32_t nb32, const int64_t nb33) {
#ifdef FLASH_ATTN_AVAILABLE

    // Skip unused kernel variants for faster compilation:
    if (use_logit_softcap && !(D == 128 || D == 256 || D == 512)) {
        GGML_UNUSED_VARS(Q, K, V, mask, sinks, KV_max, dst, dst_meta, scale,
            max_bias, m0, m1, n_head_log2, logit_softcap,
            ne00, ne01, ne02, ne03,
                  nb01, nb02, nb03,
            ne10, ne11, ne12, ne13,
                  nb11, nb12, nb13,
                  nb21, nb22, nb23,
                  ne31, ne32, ne33,
                  nb31, nb32, nb33);
        NO_DEVICE_CODE;
        return;
    }

    //In this kernel Q, K, V are matrices while i, j, k are matrix indices.

    constexpr int cpy_nb = ggml_cuda_get_max_cpy_bytes();
    constexpr int cpy_ne = cpy_nb / 4;

#ifdef GGML_USE_HIP
#ifdef RDNA
    constexpr int nthreads_KQ_q = 2;
#else
    constexpr int nthreads_KQ_q = 4;
#endif // RDNA
    constexpr int nthreads_V_q  = (D/4 < 32 ? D/4 : 32);
#else
    constexpr int nthreads_KQ_q = (D/4 < 32 ? D/4 : 32);
    constexpr int nthreads_V_q  = (D/4 < 32 ? D/4 : 32);
#endif // GGML_USE_HIP

    constexpr int nthreads    = ggml_cuda_fattn_vec_get_nthreads_device();
    // ALL turbo types use nthreads_KQ=8 for better warp-level ILP at long context (S24B).
    // nthreads_KQ_q=32 gives 1 KQ dot per warp; 8 gives 4 interleaved dots → better latency hiding.
    // Dead End #19: nthreads_KQ=32 for turbo3 was -17% at 32K. turbo4/turbo1.5 had the same bug.
    constexpr bool K_is_unquantized = (type_K == GGML_TYPE_F16 || type_K == GGML_TYPE_BF16);
    constexpr bool V_is_unquantized = (type_V == GGML_TYPE_F16 || type_V == GGML_TYPE_BF16);
    constexpr bool K_is_turbo3_0    = (type_K == GGML_TYPE_TURBO3_0);
    constexpr bool K_is_turbo3_tcq  = (type_K == GGML_TYPE_TURBO3_TCQ);
    constexpr bool V_is_turbo3_0    = (type_V == GGML_TYPE_TURBO3_0);
    constexpr bool K_is_turbo2_0    = (type_K == GGML_TYPE_TURBO2_0);
    constexpr bool V_is_turbo2_0    = (type_V == GGML_TYPE_TURBO2_0);
    constexpr bool K_is_turbo       = (type_K == GGML_TYPE_TURBO3_0 || type_K == GGML_TYPE_TURBO2_0 ||
                                       type_K == GGML_TYPE_TURBO4_0 || type_K == GGML_TYPE_TURBO1_5 ||
                                       type_K == GGML_TYPE_TURBO3_TCQ || type_K == GGML_TYPE_TURBO2_TCQ);
    constexpr int nthreads_KQ = K_is_unquantized ? 128 / cpy_nb : (K_is_turbo3_tcq ? (D >= 256 ? 128 / cpy_nb : 64 / cpy_nb) : (K_is_turbo3_0 || K_is_turbo2_0 ? 16 / cpy_nb : (K_is_turbo ? 128 / cpy_nb : nthreads_KQ_q)));
    constexpr int nthreads_V  = V_is_unquantized ? 128 / cpy_nb : (V_is_turbo3_0 || V_is_turbo2_0 ? 8 : nthreads_V_q);

    static_assert(WARP_SIZE % nthreads_KQ == 0, "bad nthreads_K");
    static_assert(WARP_SIZE % nthreads_V  == 0, "bad nthreads_V");

    constexpr int V_rows_per_thread = V_is_unquantized ? 2*cpy_ne : 4;
    constexpr int V_cols_per_iter   = WARP_SIZE / nthreads_V;

    constexpr bool K_is_tcq = (type_K == GGML_TYPE_TURBO3_TCQ || type_K == GGML_TYPE_TURBO2_TCQ);
    constexpr bool Q_q8_1 = !K_is_unquantized && !K_is_tcq;

    const int ic0 = blockIdx.x * ncols; // Index of the Q/QKV column to work on.

    const int sequence = blockIdx.z / ne02;
    const int head = blockIdx.z - sequence*ne02;
    const int gqa_ratio = ne02 / ne12; // With grouped query attention there are > 1 Q matrices per K, V matrix.
    Q += nb03*sequence + nb02* head              + nb01*ic0;
    K += nb13*sequence + nb12*(head / gqa_ratio);
    V += nb23*sequence + nb22*(head / gqa_ratio);

    const half * maskh  = (const half  *) (mask + nb33*(sequence % ne33) + nb31*ic0);

    const float slope = get_alibi_slope(max_bias, head, n_head_log2, m0, m1);

    static_assert(D % (2*WARP_SIZE) == 0, "D not divisible by 2*WARP_SIZE == 64.");
    constexpr int nwarps = nthreads / WARP_SIZE;
    const int tid = WARP_SIZE*threadIdx.y + threadIdx.x;
    __builtin_assume(tid < nthreads);

    constexpr int ne_KQ      = ncols*D;
    constexpr int ne_combine = nwarps*V_cols_per_iter*D;
#ifdef V_DOT2_F32_F16_AVAILABLE
    half2            VKQ[ncols][(D/2)/nthreads_V] = {{{0.0f, 0.0f}}};
    __shared__ half   KQ[ne_KQ > ne_combine ? ne_KQ : ne_combine];
#else
    float2           VKQ[ncols][(D/2)/nthreads_V] = {{{0.0f, 0.0f}}};
    __shared__ float  KQ[ne_KQ > ne_combine ? ne_KQ : ne_combine];
#endif // V_DOT2_F32_F16_AVAILABLE

    // TCQ codebook in shared memory for K and V dequant.
    // Constant memory serializes when threads hit different 32B cache lines;
    // shared memory gives full 32-bank parallel access for random lookups.
    // When K and V use different TCQ types, load both codebooks separately.
    constexpr bool K_is_tcq3 = type_K == GGML_TYPE_TURBO3_TCQ;
    constexpr bool K_is_tcq2 = type_K == GGML_TYPE_TURBO2_TCQ;
    constexpr bool V_is_tcq3 = type_V == GGML_TYPE_TURBO3_TCQ;
    constexpr bool V_is_tcq2 = type_V == GGML_TYPE_TURBO2_TCQ;
    constexpr bool K_tcq_uses_smem_cb = (K_is_tcq3 || K_is_tcq2) && type_V != GGML_TYPE_Q8_0;
    constexpr int smem_cb_K_size = K_tcq_uses_smem_cb ? (K_is_tcq3 ? 512 : 256) : 0;
    constexpr int smem_cb_V_size = V_is_tcq3 ? 512 : (V_is_tcq2 ? 256 : 0);
    constexpr bool share_cb = (K_is_tcq3 && V_is_tcq3) || (K_is_tcq2 && V_is_tcq2);
    constexpr int smem_cb_total = share_cb ? smem_cb_K_size : (smem_cb_K_size + smem_cb_V_size);
    __shared__ float smem_codebook_buf[smem_cb_total > 0 ? smem_cb_total : 1];
#define FATTN_SMEM_CODEBOOK_K smem_codebook_buf
#define FATTN_SMEM_CODEBOOK_V (share_cb ? smem_codebook_buf : (smem_codebook_buf + smem_cb_K_size))
    if constexpr (smem_cb_K_size > 0) {
        const float * cb_K_src = K_is_tcq3 ? d_turbo3_tcq_codebook : d_turbo2_tcq_codebook;
        for (int i = tid; i < smem_cb_K_size; i += nthreads) {
            FATTN_SMEM_CODEBOOK_K[i] = cb_K_src[i];
        }
    }
    if constexpr (smem_cb_V_size > 0 && !share_cb) {
        const float * cb_V_src = V_is_tcq3 ? d_turbo3_tcq_codebook : d_turbo2_tcq_codebook;
        for (int i = tid; i < smem_cb_V_size; i += nthreads) {
            FATTN_SMEM_CODEBOOK_V[i] = cb_V_src[i];
        }
    }
    if constexpr (smem_cb_total > 0) {
        __syncthreads();
    }

    // Sparse V: skip V dequant for positions with negligible attention weights.
    // Keep the validated conservative threshold to avoid quality regressions.
    constexpr float sparse_v_threshold_f = 1e-6f;
#ifdef V_DOT2_F32_F16_AVAILABLE
    const     half  sparse_v_threshold_h = __float2half(sparse_v_threshold_f);
#endif

    float KQ_max[ncols];
    float KQ_sum[ncols];
#pragma unroll
    for (int j = 0; j < ncols; ++j) {
        KQ_max[j] = -FLT_MAX/2.0f;
        KQ_sum[j] = 0.0f;
    }

    // Convert Q to float2 (f16 K) or q8_1 (quantized K) and store in registers:
#ifdef V_DOT2_F32_F16_AVAILABLE
    half2  Q_reg[ncols][(D/2)/nthreads_KQ]; // Will be initialized completely.
#else
    __align__(16) float2 Q_reg[ncols][(D/2)/nthreads_KQ] = {{{0.0f, 0.0f}}}; // May be only partially initialized.
#endif // V_DOT2_F32_F16_AVAILABLE
    int    Q_i32[ncols][1 > D/(sizeof(int)*nthreads_KQ) ? 1 : D/(sizeof(int)*nthreads_KQ)];
    float2  Q_ds[ncols][1 > D/(sizeof(int)*nthreads_KQ) ? 1 : D/(sizeof(int)*nthreads_KQ)];
    if constexpr (Q_q8_1) {
#pragma unroll
        for (int j0 = 0; j0 < ncols; j0 += nwarps) {
            const int j = j0 + threadIdx.y;

            if (j0 + nwarps > ncols && j >= ncols) {
                break;
            }

            // Reuse KQ as temporary storage for converting Q to q8_1:
            int    * tmp_q_i32 = (int    *) &KQ[j*D];
            float2 * tmp_q_ds  = (float2 *) (tmp_q_i32 + D/sizeof(int));

            // Set memory to zero if out of bounds:
            if (ncols > 1 && ic0 + j >= int(ne01.z)) {
#pragma unroll
                for (int i0 = 0; i0 < int(D/sizeof(int)); i0 += WARP_SIZE) {
                    const int i = i0 + threadIdx.x;

                    if (i0 + WARP_SIZE <= int(D/sizeof(int)) || i < int(D/sizeof(int))) {
                        tmp_q_i32[i] = 0;
                    }
                }
                if (threadIdx.x < D/QK8_1) {
                    tmp_q_ds[threadIdx.x] = make_float2(0.0f, 0.0f);
                }
            } else {
                const float * Q_f = (const float *) (Q + j*nb01);
                constexpr int nthreads_quantize = D/sizeof(int) < WARP_SIZE ? D/sizeof(int) : WARP_SIZE;
#pragma unroll
                for (int i0 = 0; i0 < int(D/sizeof(int)); i0 += nthreads_quantize) {
                    quantize_q8_1_to_shared<float2, nthreads_quantize>
                        (Q_f + i0*sizeof(int), scale, tmp_q_i32 + i0, tmp_q_ds + i0/QI8_1);
                }
            }
        }

        __syncthreads();

#pragma unroll
        for (int j = 0; j < ncols; ++j) {
            int    * tmp_q_i32 = (int    *) &KQ[j*D];
            float2 * tmp_q_ds  = (float2 *) (tmp_q_i32 + D/sizeof(int));

#pragma unroll
            for (int i0 = 0; i0 < int(D/sizeof(int)); i0 += nthreads_KQ) {
                const int i = i0 + (nthreads_KQ == WARP_SIZE ? threadIdx.x : threadIdx.x % nthreads_KQ);

                Q_i32[j][i0/nthreads_KQ] = tmp_q_i32[i];
                Q_ds[j][i0/nthreads_KQ]  = tmp_q_ds[i/QI8_1];
            }
        }

        __syncthreads();
    } else {
#ifdef V_DOT2_F32_F16_AVAILABLE
        const half2 scale_h2 = make_half2(scale, scale);
#pragma unroll
        for (int j = 0; j < ncols; ++j) {
            const float2 * Q_j = (const float2 *) (Q + j*nb01);
#pragma unroll
            for (int i0 = 0; i0 < D/2; i0 += nthreads_KQ*cpy_ne) {
                const int i = i0 + (nthreads_KQ == WARP_SIZE ? threadIdx.x : threadIdx.x % nthreads_KQ)*cpy_ne;

                __align__(16) float2 tmp[cpy_ne] = {{0.0f, 0.0f}};
                if (ncols == 1 || ic0 + j < int(ne01.z)) {
                    ggml_cuda_memcpy_1<cpy_nb>(tmp,            &Q_j[i]);
                    ggml_cuda_memcpy_1<cpy_nb>(tmp + cpy_ne/2, &Q_j[i + cpy_ne/2]);
                }
#pragma unroll
                for (int i1 = 0; i1 < cpy_ne; ++i1) {
                    Q_reg[j][i0/nthreads_KQ + i1] = make_half2(tmp[i1].x, tmp[i1].y);
                }
            }
#pragma unroll
            for (int k = 0; k < (D/2)/nthreads_KQ; ++k) {
                Q_reg[j][k] *= scale_h2;
            }
        }
#else
#pragma unroll
        for (int j = 0; j < ncols; ++j) {
            const float2 * Q_j = (const float2 *) (Q + j*nb01);
#pragma unroll
            for (int i0 = 0; i0 < D/2; i0 += nthreads_KQ*cpy_ne) {
                const int i = i0 + (nthreads_KQ == WARP_SIZE ? threadIdx.x : threadIdx.x % nthreads_KQ)*cpy_ne;
                if (ncols == 1 || ic0 + j < int(ne01.z)) {
                    ggml_cuda_memcpy_1<cpy_nb>(&Q_reg[j][i0/nthreads_KQ],            &Q_j[i]);
                    ggml_cuda_memcpy_1<cpy_nb>(&Q_reg[j][i0/nthreads_KQ + cpy_ne/2], &Q_j[i + cpy_ne/2]);
                }
            }
#pragma unroll
            for (int k = 0; k < (D/2)/nthreads_KQ; ++k) {
                Q_reg[j][k].x *= scale;
                Q_reg[j][k].y *= scale;
            }
        }
#endif // V_DOT2_F32_F16_AVAILABLE
    }

    const int k_VKQ_max = KV_max ? KV_max[sequence*gridDim.x + blockIdx.x] : ne11;
    K     += blockIdx.y*nthreads * nb11;
    V     += blockIdx.y*nthreads * nb21;
    maskh += blockIdx.y*nthreads;
    for (int k_VKQ_0 = blockIdx.y*nthreads; k_VKQ_0 < k_VKQ_max; k_VKQ_0 += gridDim.y*nthreads,
             // Increment pointers after each loop:
             K += gridDim.y*nthreads*nb11, V += gridDim.y*nthreads*nb21, maskh += gridDim.y*nthreads) {

        // L2 prefetch: issue hints for the NEXT outer iteration's K and V blocks
        // while computing the current iteration. Hides GDDR latency (~300-400 cycles).
        // Available on SM50+, no correctness impact (hint only).
#if __CUDA_ARCH__ >= 500
        if (k_VKQ_0 + gridDim.y*nthreads < k_VKQ_max) {
            const char * K_next = K + gridDim.y*nthreads*nb11;
            const char * V_next = V + gridDim.y*nthreads*nb21;
            // Prefetch first few K and V cache lines of next chunk (128 bytes each)
            asm volatile("prefetch.global.L2 [%0];" :: "l"(K_next));
            asm volatile("prefetch.global.L2 [%0];" :: "l"(K_next + nb11));
            asm volatile("prefetch.global.L2 [%0];" :: "l"(V_next));
            asm volatile("prefetch.global.L2 [%0];" :: "l"(V_next + nb21));
        }
#endif

        // Calculate KQ tile and keep track of new maximum KQ values:
        float KQ_reg[ncols]; // KQ in registers.

        float KQ_max_new[ncols];
#pragma unroll
        for (int j = 0; j < ncols; ++j) {
            KQ_max_new[j] = KQ_max[j];
        }

#pragma unroll
        for (int i_KQ_0 = 0; i_KQ_0 < nthreads_KQ; ++i_KQ_0) {
            const int i_KQ = threadIdx.y*WARP_SIZE + (nthreads_KQ == WARP_SIZE ? 0 : (threadIdx.x & ~(nthreads_KQ-1))) + i_KQ_0;

#pragma unroll
            for (int j = 0; j < ncols; ++j) {
                float sum;
                // Turbo attention: sinks (fp16) check, then LUT or standard vec_dot
                if constexpr (type_K == GGML_TYPE_TURBO3_0 || type_K == GGML_TYPE_TURBO4_0 ||
                              type_K == GGML_TYPE_TURBO2_0 || type_K == GGML_TYPE_TURBO1_5) {
                    const int kv_pos = k_VKQ_0 + i_KQ;
                    if (d_fattn_sink_n > 0 && kv_pos < d_fattn_sink_n && d_fattn_sink_K_buf != nullptr) {
                        const int kv_head = head / gqa_ratio;
                        const char * sink_K = (const char *)(d_fattn_sink_K_buf + kv_pos * d_fattn_sink_ne0 + kv_head * D);
                        sum = vec_dot_fattn_vec_KQ_f16<D, nthreads_KQ>(sink_K, Q_reg[j], Q_i32[j], Q_ds[j]);
                    } else if constexpr (type_K == GGML_TYPE_TURBO3_0) {
                        sum = vec_dot_fattn_vec_KQ_turbo3_0_lean<D, nthreads_KQ>(
                            K + i_KQ*nb11, Q_reg[j], Q_i32[j], Q_ds[j]);
                    } else {
                        sum = get_vec_dot_KQ_fattn<type_K, D, nthreads_KQ>()(K + i_KQ*nb11, Q_reg[j], Q_i32[j], Q_ds[j]);
                    }
                } else if constexpr (type_K == GGML_TYPE_TURBO3_TCQ) {
                    // Sink fast-path: positions < TURBO_SINK_SIZE use captured fp16 K
                    // (matches turbo3_0 pattern at the top of this if/else chain).
                    const int kv_pos = k_VKQ_0 + i_KQ;
                    if (d_fattn_sink_n > 0 && kv_pos < d_fattn_sink_n && d_fattn_sink_K_buf != nullptr) {
                        const int kv_head = head / gqa_ratio;
                        const char * sink_K = (const char *)(d_fattn_sink_K_buf + kv_pos * d_fattn_sink_ne0 + kv_head * D);
                        sum = vec_dot_fattn_vec_KQ_f16<D, nthreads_KQ>(sink_K, Q_reg[j], Q_i32[j], Q_ds[j]);
                    } else {
                        sum = vec_dot_fattn_vec_KQ_turbo3_tcq_decode<D, nthreads_KQ>(
                            K + i_KQ*nb11, Q_reg[j], Q_i32[j], Q_ds[j]);
                    }
                } else if constexpr (type_K == GGML_TYPE_TURBO2_TCQ) {
                    const int kv_pos = k_VKQ_0 + i_KQ;
                    if (d_fattn_sink_n > 0 && kv_pos < d_fattn_sink_n && d_fattn_sink_K_buf != nullptr) {
                        const int kv_head = head / gqa_ratio;
                        const char * sink_K = (const char *)(d_fattn_sink_K_buf + kv_pos * d_fattn_sink_ne0 + kv_head * D);
                        sum = vec_dot_fattn_vec_KQ_f16<D, nthreads_KQ>(sink_K, Q_reg[j], Q_i32[j], Q_ds[j]);
                    } else {
                        if constexpr (type_V == GGML_TYPE_Q8_0) {
                            sum = vec_dot_fattn_vec_KQ_turbo2_tcq<D, nthreads_KQ>(
                                K + i_KQ*nb11, Q_reg[j], Q_i32[j], Q_ds[j]);
                        } else {
                            sum = vec_dot_fattn_vec_KQ_turbo2_tcq_cb<D, nthreads_KQ>(
                                K + i_KQ*nb11, Q_reg[j], Q_i32[j], Q_ds[j], FATTN_SMEM_CODEBOOK_K);
                        }
                    }
                } else {
                    sum = get_vec_dot_KQ_fattn<type_K, D, nthreads_KQ>()(K + i_KQ*nb11, Q_reg[j], Q_i32[j], Q_ds[j]);
                }
                sum = warp_reduce_sum<nthreads_KQ>(sum);

                if (use_logit_softcap) {
                    sum = logit_softcap*tanhf(sum);
                }

                if (mask && (ncols == 1 || ic0 + j < int(ne01.z))) {
                    sum += slope*__half2float(maskh[j*ne11 + i_KQ]);
                }

                KQ_max_new[j] = fmaxf(KQ_max_new[j], sum + FATTN_KQ_MAX_OFFSET);

                if ((nthreads_KQ == WARP_SIZE ? threadIdx.x : threadIdx.x % nthreads_KQ) == uint32_t(i_KQ_0)) {
                    KQ_reg[j] = sum;
                }
            }
        }

#pragma unroll
        for (int j = 0; j < ncols; ++j) {
#pragma unroll
            for (int offset = nthreads_KQ; offset < WARP_SIZE; offset <<= 1) {
                KQ_max_new[j] = fmaxf(KQ_max_new[j], __shfl_xor_sync(0xFFFFFFFF, KQ_max_new[j], offset, WARP_SIZE));
            }
            const float KQ_max_scale = __expf(KQ_max[j] - KQ_max_new[j]);
            KQ_max[j] = KQ_max_new[j];

            KQ_reg[j] = __expf(KQ_reg[j] - KQ_max[j]);
            KQ_sum[j] = KQ_sum[j]*KQ_max_scale + KQ_reg[j];
            KQ[j*nthreads + tid] = KQ_reg[j];

#ifdef V_DOT2_F32_F16_AVAILABLE
            const half2 KQ_max_scale_h2 = make_half2(KQ_max_scale, KQ_max_scale);
#pragma unroll
            for (int i_VKQ_0 = 0; i_VKQ_0 < D/2; i_VKQ_0 += nthreads_V) {
                VKQ[j][i_VKQ_0/nthreads_V] *= KQ_max_scale_h2;
            }
#else
#pragma unroll
            for (int i_VKQ_0 = 0; i_VKQ_0 < D/2; i_VKQ_0 += nthreads_V) {
                VKQ[j][i_VKQ_0/nthreads_V].x *= KQ_max_scale;
                VKQ[j][i_VKQ_0/nthreads_V].y *= KQ_max_scale;
            }
#endif // V_DOT2_F32_F16_AVAILABLE
        }

#ifndef GGML_USE_HIP
        __syncwarp();
#endif // GGML_USE_HIP

#pragma unroll
        for (int k0 = 0; k0 < WARP_SIZE; k0 += V_cols_per_iter) {
            const int k = threadIdx.y*WARP_SIZE + k0 + (nthreads_V == WARP_SIZE ? 0 : threadIdx.x / nthreads_V);

#ifdef V_DOT2_F32_F16_AVAILABLE
            half2 KQ_k[ncols];
#pragma unroll
            for (int j = 0; j < ncols; ++j) {
                KQ_k[j] = __half2half2(KQ[j*nthreads + k]);
            }

            // Sparse V: skip V dequant if all attention weights for this position are negligible
            {
                bool dominated = true;
#pragma unroll
                for (int j = 0; j < ncols; ++j) {
                    if (__hgt(__low2half(KQ_k[j]), sparse_v_threshold_h)) { dominated = false; break; }
                }
                if (dominated) { continue; }
            }

#pragma unroll
            for (int i_VKQ_0 = 0; i_VKQ_0 < D/2; i_VKQ_0 += nthreads_V*V_rows_per_thread/2) {
                half2 tmp[V_rows_per_thread/2];
                if constexpr (type_V == GGML_TYPE_BF16) {
                    float2 tmp_f[V_rows_per_thread/2];
                    get_dequantize_V<type_V, float, V_rows_per_thread>()(V + k*nb21, tmp_f,
                        2*i_VKQ_0 + (nthreads_V == WARP_SIZE ? threadIdx.x : threadIdx.x % nthreads_V)*V_rows_per_thread);
#pragma unroll
                    for (int i_VKQ_1 = 0; i_VKQ_1 < V_rows_per_thread/2; ++i_VKQ_1) {
                        tmp[i_VKQ_1] = __float22half2_rn(tmp_f[i_VKQ_1]);
                    }
                } else if constexpr (type_V == GGML_TYPE_TURBO3_TCQ) {
                    dequantize_V_turbo3_tcq_cb<half, V_rows_per_thread>(V + k*nb21, tmp,
                        2*i_VKQ_0 + (nthreads_V == WARP_SIZE ? threadIdx.x : threadIdx.x % nthreads_V)*V_rows_per_thread, FATTN_SMEM_CODEBOOK_V);
                } else if constexpr (type_V == GGML_TYPE_TURBO2_TCQ) {
                    dequantize_V_turbo2_tcq_cb<half, V_rows_per_thread>(V + k*nb21, tmp,
                        2*i_VKQ_0 + (nthreads_V == WARP_SIZE ? threadIdx.x : threadIdx.x % nthreads_V)*V_rows_per_thread, FATTN_SMEM_CODEBOOK_V);
                } else {
                    get_dequantize_V<type_V, half, V_rows_per_thread>()(V + k*nb21, tmp,
                        2*i_VKQ_0 + (nthreads_V == WARP_SIZE ? threadIdx.x : threadIdx.x % nthreads_V)*V_rows_per_thread);
                }
#pragma unroll
                for (int i_VKQ_1 = 0; i_VKQ_1 < V_rows_per_thread/2; ++i_VKQ_1) {
#pragma unroll
                    for (int j = 0; j < ncols; ++j) {
                        VKQ[j][i_VKQ_0/nthreads_V + i_VKQ_1] += tmp[i_VKQ_1]*KQ_k[j];
                    }
                }
            }
#else
            float KQ_k[ncols];
#pragma unroll
            for (int j = 0; j < ncols; ++j) {
                KQ_k[j] = KQ[j*nthreads + k];
            }

            // Sparse V: skip V dequant if all attention weights for this position are negligible
            {
                bool dominated = true;
#pragma unroll
                for (int j = 0; j < ncols; ++j) {
                    if (KQ_k[j] >= sparse_v_threshold_f) { dominated = false; break; }
                }
                if (dominated) { continue; }
            }

#pragma unroll
            for (int i_VKQ_0 = 0; i_VKQ_0 < D/2; i_VKQ_0 += nthreads_V*V_rows_per_thread/2) {
                float2 tmp[V_rows_per_thread/2];
                if constexpr (type_V == GGML_TYPE_TURBO3_TCQ) {
                    dequantize_V_turbo3_tcq_cb<float, V_rows_per_thread>(V + k*nb21, tmp,
                        2*i_VKQ_0 + (nthreads_V == WARP_SIZE ? threadIdx.x : threadIdx.x % nthreads_V)*V_rows_per_thread, FATTN_SMEM_CODEBOOK_V);
                } else if constexpr (type_V == GGML_TYPE_TURBO2_TCQ) {
                    dequantize_V_turbo2_tcq_cb<float, V_rows_per_thread>(V + k*nb21, tmp,
                        2*i_VKQ_0 + (nthreads_V == WARP_SIZE ? threadIdx.x : threadIdx.x % nthreads_V)*V_rows_per_thread, FATTN_SMEM_CODEBOOK_V);
                } else {
                    get_dequantize_V<type_V, float, V_rows_per_thread>()(V + k*nb21, tmp,
                        2*i_VKQ_0 + (nthreads_V == WARP_SIZE ? threadIdx.x : threadIdx.x % nthreads_V)*V_rows_per_thread);
                }
#pragma unroll
                for (int i_VKQ_1 = 0; i_VKQ_1 < V_rows_per_thread/2; ++i_VKQ_1) {
#pragma unroll
                    for (int j = 0; j < ncols; ++j) {
                        VKQ[j][i_VKQ_0/nthreads_V + i_VKQ_1].x += tmp[i_VKQ_1].x*KQ_k[j];
                        VKQ[j][i_VKQ_0/nthreads_V + i_VKQ_1].y += tmp[i_VKQ_1].y*KQ_k[j];
                    }
                }
            }
#endif // V_DOT2_F32_F16_AVAILABLE
        }
    }

    if (sinks && blockIdx.y == 0) {
        const float sink = ((const float *) sinks)[head];

#pragma unroll
        for (int j0 = 0; j0 < ncols; j0 += nwarps) {
            const int j = j0 + threadIdx.y;

            if (j0 + nwarps > ncols && j >= ncols) {
                break;
            }

            const float kqmax_new_j = fmaxf(sink, KQ_max[j]);
            const float KQ_max_scale = __expf(KQ_max[j] - kqmax_new_j);
            KQ_max[j] = kqmax_new_j;

            KQ_sum[j] = KQ_sum[j]*KQ_max_scale + (threadIdx.x == 0 ? __expf(sink - KQ_max[j]) : 0.0f);

#ifdef V_DOT2_F32_F16_AVAILABLE
            const half2 KQ_max_scale_h2 = make_half2(KQ_max_scale, KQ_max_scale);
#pragma unroll
            for (int i_VKQ_0 = 0; i_VKQ_0 < D/2; i_VKQ_0 += nthreads_V) {
                VKQ[j][i_VKQ_0/nthreads_V] *= KQ_max_scale_h2;
            }
#else
#pragma unroll
            for (int i_VKQ_0 = 0; i_VKQ_0 < D/2; i_VKQ_0 += nthreads_V) {
                VKQ[j][i_VKQ_0/nthreads_V].x *= KQ_max_scale;
                VKQ[j][i_VKQ_0/nthreads_V].y *= KQ_max_scale;
            }
#endif // V_DOT2_F32_F16_AVAILABLE
        }
    }

    __shared__ float KQ_max_shared[ncols][WARP_SIZE];
    __shared__ float KQ_sum_shared[ncols][WARP_SIZE];
#pragma unroll
    for (int j = 0; j < ncols; ++j) {
        if (threadIdx.y == 0) {
            KQ_max_shared[j][threadIdx.x] = -FLT_MAX/2.0f;
            KQ_sum_shared[j][threadIdx.x] = 0.0f;
        }
    }

    __syncthreads();

#pragma unroll
    for (int j = 0; j < ncols; ++j) {
        if (threadIdx.x == 0) {
            KQ_max_shared[j][threadIdx.y] = KQ_max[j];
        }
    }
    __syncthreads();

#pragma unroll
    for (int j_VKQ = 0; j_VKQ < ncols; ++j_VKQ) {
        if (ncols > 1 && ic0 + j_VKQ >= int(ne01.z)) {
            break;
        }

        float kqmax_new = KQ_max_shared[j_VKQ][threadIdx.x];
        kqmax_new = warp_reduce_max(kqmax_new);
        const float kqmax_scale = __expf(KQ_max[j_VKQ] - kqmax_new);
        KQ_max[j_VKQ] = kqmax_new;

#ifdef V_DOT2_F32_F16_AVAILABLE
        half2 * VKQ_tmp = (half2 *) KQ + threadIdx.y*(V_cols_per_iter*D/2)
            + (nthreads_V == WARP_SIZE ? 0 : threadIdx.x / nthreads_V)*(D/2);

        const half2 kqmax_scale_h2 = make_half2(kqmax_scale, kqmax_scale);
#pragma unroll
        for (int i_VKQ_0 = 0; i_VKQ_0 < D/2; i_VKQ_0 += nthreads_V) {
            VKQ[j_VKQ][i_VKQ_0/nthreads_V] *= kqmax_scale_h2;
        }
#pragma unroll
        for (int i_VKQ_0 = 0; i_VKQ_0 < D/2; i_VKQ_0 += nthreads_V*V_rows_per_thread/2) {
            const int i_VKQ = i_VKQ_0 + (nthreads_V == WARP_SIZE ? threadIdx.x : threadIdx.x % nthreads_V)*(V_rows_per_thread/2);

            ggml_cuda_memcpy_1<V_rows_per_thread*sizeof(half)>(VKQ_tmp + i_VKQ, &VKQ[j_VKQ][i_VKQ_0/nthreads_V]);
        }
#else
        float2 * VKQ_tmp = (float2 *) KQ + threadIdx.y*(V_cols_per_iter*D/2)
            + (nthreads_V == WARP_SIZE ? 0 : threadIdx.x / nthreads_V)*(D/2);

#pragma unroll
        for (int i_VKQ_0 = 0; i_VKQ_0 < D/2; i_VKQ_0 += nthreads_V) {
            VKQ[j_VKQ][i_VKQ_0/nthreads_V].x *= kqmax_scale;
            VKQ[j_VKQ][i_VKQ_0/nthreads_V].y *= kqmax_scale;
        }
#pragma unroll
        for (int i_VKQ_0 = 0; i_VKQ_0 < D/2; i_VKQ_0 += nthreads_V*V_rows_per_thread/2) {
            const int i_VKQ = i_VKQ_0 + (nthreads_V == WARP_SIZE ? threadIdx.x : threadIdx.x % nthreads_V)*(V_rows_per_thread/2);

            ggml_cuda_memcpy_1<V_rows_per_thread/2*sizeof(float)>(VKQ_tmp + i_VKQ,                       &VKQ[j_VKQ][i_VKQ_0/nthreads_V]);
            ggml_cuda_memcpy_1<V_rows_per_thread/2*sizeof(float)>(VKQ_tmp + i_VKQ + V_rows_per_thread/4, &VKQ[j_VKQ][i_VKQ_0/nthreads_V + V_rows_per_thread/4]);
        }
#endif // V_DOT2_F32_F16_AVAILABLE

        KQ_sum[j_VKQ] *= kqmax_scale;
        KQ_sum[j_VKQ] = warp_reduce_sum(KQ_sum[j_VKQ]);
        if (threadIdx.x == 0) {
            KQ_sum_shared[j_VKQ][threadIdx.y] = KQ_sum[j_VKQ];
        }

        __syncthreads();

        if (nthreads <= D || tid < D) {
            KQ_sum[j_VKQ] = KQ_sum_shared[j_VKQ][threadIdx.x];
            KQ_sum[j_VKQ] = warp_reduce_sum(KQ_sum[j_VKQ]);

#pragma unroll
            for (int i0 = 0; i0 < D; i0 += nthreads) {
                float dst_val = 0;
#pragma unroll
                for (int w = 0; w < nwarps; ++w) {
#pragma unroll
                    for (int v = 0; v < V_cols_per_iter; ++v) {
                        dst_val += float(KQ[w*V_cols_per_iter*D + v*D + i0 + tid]);
                    }
                }
                if (gridDim.y == 1) {
                    dst_val /= KQ_sum[j_VKQ];
                }
                dst[(((sequence*int(ne01.z) + ic0 + j_VKQ)*ne02 + head)*gridDim.y + blockIdx.y)*D + i0 + tid] = dst_val;
            }
        }

        if (j_VKQ < ncols-1) {
            __syncthreads();
        }

    }

    if (gridDim.y != 1 && tid < ncols && (ncols == 1 || ic0 + tid < int(ne01.z))) {
        dst_meta[((sequence*int(ne01.z) + ic0 + tid)*ne02 + head)*gridDim.y + blockIdx.y] = make_float2(KQ_max[tid], KQ_sum[tid]);
    }
#else
    GGML_UNUSED_VARS(Q, K, V, mask, sinks, KV_max, dst, dst_meta, scale,
        max_bias, m0, m1, n_head_log2, logit_softcap,
        ne00, ne01, ne02, ne03,
              nb01, nb02, nb03,
        ne10, ne11, ne12, ne13,
              nb11, nb12, nb13,
              nb21, nb22, nb23,
              ne31, ne32, ne33,
              nb31, nb32, nb33);
    NO_DEVICE_CODE;
#endif // FLASH_ATTN_AVAILABLE
}
#undef FATTN_SMEM_CODEBOOK_K
#undef FATTN_SMEM_CODEBOOK_V
#ifdef __clang__
#pragma clang diagnostic pop
#endif // __clang__

template <int D, int cols_per_block, ggml_type type_K, ggml_type type_V, bool use_logit_softcap>
void ggml_cuda_flash_attn_ext_vec_case_impl(ggml_backend_cuda_context & ctx, ggml_tensor * dst) {
    // Turbo attention sinks: set device memory state for K scoring in VEC kernel.
    // Uses __device__ variables updated via cudaGetSymbolAddress + cudaMemcpyAsync
    // (graph-capturable). Previous __managed__ approach crashed on SM86.
    if constexpr (type_K == GGML_TYPE_TURBO3_0 || type_K == GGML_TYPE_TURBO4_0 ||
                  type_K == GGML_TYPE_TURBO2_0 || type_K == GGML_TYPE_TURBO1_5 ||
                  type_K == GGML_TYPE_TURBO3_TCQ || type_K == GGML_TYPE_TURBO2_TCQ) {
        const int ss = turbo_sink_size();
        if (ss > 0) {
            const ggml_tensor * K = dst->src[1];
            int64_t k_ne0_full = 0;
            half * k_buf = turbo_sink_lookup_buf((void *)K->data, &k_ne0_full);
            const half * k_buf_const = k_buf;

            // Get device addresses once (cached per TU via static locals)
            static void * d_addr_buf = nullptr;
            static void * d_addr_n   = nullptr;
            static void * d_addr_ne0 = nullptr;
            if (!d_addr_buf) {
                CUDA_CHECK(cudaGetSymbolAddress(&d_addr_buf, d_fattn_sink_K_buf));
                CUDA_CHECK(cudaGetSymbolAddress(&d_addr_n,   d_fattn_sink_n));
                CUDA_CHECK(cudaGetSymbolAddress(&d_addr_ne0, d_fattn_sink_ne0));
            }

            // cudaMemcpyAsync is graph-capturable (unlike cudaMemcpyToSymbolAsync).
            // Use aligned struct — unaligned stack vars cause segfault on SM89 (Ada)
            // for certain sink sizes {1, 4, 16} due to L1 cache coherency issues.
            struct __align__(16) sink_async_state {
                const half * buf;
                int64_t      ne0;
                int          n;
            };
            static sink_async_state sink_state;
            sink_state = {k_buf_const, k_ne0_full, ss};
            CUDA_CHECK(cudaMemcpyAsync(d_addr_buf, &sink_state.buf, sizeof(const half *), cudaMemcpyHostToDevice, ctx.stream()));
            CUDA_CHECK(cudaMemcpyAsync(d_addr_ne0, &sink_state.ne0, sizeof(int64_t),      cudaMemcpyHostToDevice, ctx.stream()));
            CUDA_CHECK(cudaMemcpyAsync(d_addr_n,   &sink_state.n,   sizeof(int),          cudaMemcpyHostToDevice, ctx.stream()));
        }
    }

    const int cc = ggml_cuda_info().devices[ggml_cuda_get_device()].cc;

    const int nthreads = ggml_cuda_fattn_vec_get_nthreads_host(cc);
    const int nwarps   = nthreads / WARP_SIZE;
    fattn_kernel_t fattn_kernel = flash_attn_ext_vec<D, cols_per_block, type_K, type_V, use_logit_softcap>;
    const bool need_f16_K = type_K == GGML_TYPE_F16;
    const bool need_f16_V = type_V == GGML_TYPE_F16;
    constexpr size_t nbytes_shared = 0;
    launch_fattn<D, cols_per_block, 1>(ctx, dst, fattn_kernel, nwarps, nbytes_shared, D, need_f16_K, need_f16_V, false);
}

template <int D, ggml_type type_K, ggml_type type_V>
void ggml_cuda_flash_attn_ext_vec_case(ggml_backend_cuda_context & ctx, ggml_tensor * dst) {
    const ggml_tensor * KQV = dst;
    const ggml_tensor * Q   = dst->src[0];

    float logit_softcap;
    memcpy(&logit_softcap, (const float *) KQV->op_params + 2, sizeof(float));

    if (Q->ne[1] == 1) {
        constexpr int cols_per_block = 1;
        if (logit_softcap == 0.0f) {
            constexpr bool use_logit_softcap = false;
            ggml_cuda_flash_attn_ext_vec_case_impl<D, cols_per_block, type_K, type_V, use_logit_softcap>(ctx, dst);
        } else {
            constexpr bool use_logit_softcap = true;
            ggml_cuda_flash_attn_ext_vec_case_impl<D, cols_per_block, type_K, type_V, use_logit_softcap>(ctx, dst);
        }
        return;
    }

    constexpr int cols_per_block = 2;
    if (logit_softcap == 0.0f) {
        constexpr bool use_logit_softcap = false;
        ggml_cuda_flash_attn_ext_vec_case_impl<D, cols_per_block, type_K, type_V, use_logit_softcap>(ctx, dst);
    } else {
        constexpr bool use_logit_softcap = true;
        ggml_cuda_flash_attn_ext_vec_case_impl<D, cols_per_block, type_K, type_V, use_logit_softcap>(ctx, dst);
    }
}

template <int D, ggml_type type_K, ggml_type type_V>
void ggml_cuda_flash_attn_ext_vec_case_no_softcap(ggml_backend_cuda_context & ctx, ggml_tensor * dst) {
    const ggml_tensor * KQV = dst;
    const ggml_tensor * Q   = dst->src[0];

    float logit_softcap;
    memcpy(&logit_softcap, (const float *) KQV->op_params + 2, sizeof(float));
    GGML_ASSERT(logit_softcap == 0.0f);

    if (Q->ne[1] == 1) {
        constexpr int cols_per_block = 1;
        constexpr bool use_logit_softcap = false;
        ggml_cuda_flash_attn_ext_vec_case_impl<D, cols_per_block, type_K, type_V, use_logit_softcap>(ctx, dst);
        return;
    }

    constexpr int cols_per_block = 2;
    constexpr bool use_logit_softcap = false;
    ggml_cuda_flash_attn_ext_vec_case_impl<D, cols_per_block, type_K, type_V, use_logit_softcap>(ctx, dst);
}

template <int D, ggml_type type_K, ggml_type type_V>
void ggml_cuda_flash_attn_ext_vec_case_decode_no_softcap(ggml_backend_cuda_context & ctx, ggml_tensor * dst) {
    const ggml_tensor * KQV = dst;
    const ggml_tensor * Q   = dst->src[0];

    float logit_softcap;
    memcpy(&logit_softcap, (const float *) KQV->op_params + 2, sizeof(float));
    GGML_ASSERT(logit_softcap == 0.0f);
    GGML_ASSERT(Q->ne[1] == 1);

    constexpr int cols_per_block = 1;
    constexpr bool use_logit_softcap = false;
    ggml_cuda_flash_attn_ext_vec_case_impl<D, cols_per_block, type_K, type_V, use_logit_softcap>(ctx, dst);
}

template <int D, ggml_type type_K, ggml_type type_V>
void ggml_cuda_flash_attn_ext_vec_case_prefill_no_softcap(ggml_backend_cuda_context & ctx, ggml_tensor * dst) {
    const ggml_tensor * KQV = dst;
    const ggml_tensor * Q   = dst->src[0];

    float logit_softcap;
    memcpy(&logit_softcap, (const float *) KQV->op_params + 2, sizeof(float));
    GGML_ASSERT(logit_softcap == 0.0f);
    GGML_ASSERT(Q->ne[1] != 1);

    constexpr int cols_per_block = 2;
    constexpr bool use_logit_softcap = false;
    ggml_cuda_flash_attn_ext_vec_case_impl<D, cols_per_block, type_K, type_V, use_logit_softcap>(ctx, dst);
}

#define DECL_FATTN_VEC_CASE(D, type_K, type_V)                              \
    template void ggml_cuda_flash_attn_ext_vec_case                         \
    <D, type_K, type_V>(ggml_backend_cuda_context & ctx, ggml_tensor * dst) \

#define DECL_FATTN_VEC_CASE_NO_SOFTCAP(D, type_K, type_V)                   \
    template void ggml_cuda_flash_attn_ext_vec_case_no_softcap              \
    <D, type_K, type_V>(ggml_backend_cuda_context & ctx, ggml_tensor * dst) \

#define DECL_FATTN_VEC_CASE_DECODE_NO_SOFTCAP(D, type_K, type_V)            \
    template void ggml_cuda_flash_attn_ext_vec_case_decode_no_softcap       \
    <D, type_K, type_V>(ggml_backend_cuda_context & ctx, ggml_tensor * dst) \

#define DECL_FATTN_VEC_CASE_PREFILL_NO_SOFTCAP(D, type_K, type_V)           \
    template void ggml_cuda_flash_attn_ext_vec_case_prefill_no_softcap      \
    <D, type_K, type_V>(ggml_backend_cuda_context & ctx, ggml_tensor * dst) \

#define EXTERN_DECL_FATTN_VEC_CASES(D, type_K)             \
    extern DECL_FATTN_VEC_CASE(D, type_K, GGML_TYPE_F16);  \
    extern DECL_FATTN_VEC_CASE(D, type_K, GGML_TYPE_Q4_0); \
    extern DECL_FATTN_VEC_CASE(D, type_K, GGML_TYPE_Q4_1); \
    extern DECL_FATTN_VEC_CASE(D, type_K, GGML_TYPE_Q5_0); \
    extern DECL_FATTN_VEC_CASE(D, type_K, GGML_TYPE_Q5_1); \
    extern DECL_FATTN_VEC_CASE(D, type_K, GGML_TYPE_Q8_0); \
    extern DECL_FATTN_VEC_CASE(D, type_K, GGML_TYPE_BF16); \

EXTERN_DECL_FATTN_VEC_CASES( 64, GGML_TYPE_F16)
EXTERN_DECL_FATTN_VEC_CASES( 64, GGML_TYPE_Q4_0)
EXTERN_DECL_FATTN_VEC_CASES( 64, GGML_TYPE_Q4_1)
EXTERN_DECL_FATTN_VEC_CASES( 64, GGML_TYPE_Q5_0)
EXTERN_DECL_FATTN_VEC_CASES( 64, GGML_TYPE_Q5_1)
EXTERN_DECL_FATTN_VEC_CASES( 64, GGML_TYPE_Q8_0)
EXTERN_DECL_FATTN_VEC_CASES( 64, GGML_TYPE_BF16)

EXTERN_DECL_FATTN_VEC_CASES(128, GGML_TYPE_F16)
EXTERN_DECL_FATTN_VEC_CASES(128, GGML_TYPE_Q4_0)
EXTERN_DECL_FATTN_VEC_CASES(128, GGML_TYPE_Q4_1)
EXTERN_DECL_FATTN_VEC_CASES(128, GGML_TYPE_Q5_0)
EXTERN_DECL_FATTN_VEC_CASES(128, GGML_TYPE_Q5_1)
EXTERN_DECL_FATTN_VEC_CASES(128, GGML_TYPE_Q8_0)
EXTERN_DECL_FATTN_VEC_CASES(128, GGML_TYPE_BF16)

EXTERN_DECL_FATTN_VEC_CASES(256, GGML_TYPE_F16)
EXTERN_DECL_FATTN_VEC_CASES(256, GGML_TYPE_Q4_0)
EXTERN_DECL_FATTN_VEC_CASES(256, GGML_TYPE_Q4_1)
EXTERN_DECL_FATTN_VEC_CASES(256, GGML_TYPE_Q5_0)
EXTERN_DECL_FATTN_VEC_CASES(256, GGML_TYPE_Q5_1)
EXTERN_DECL_FATTN_VEC_CASES(256, GGML_TYPE_Q8_0)
EXTERN_DECL_FATTN_VEC_CASES(256, GGML_TYPE_BF16)

// Macro for extern declarations with D=64, 128, 256, 512
#define EXTERN_DECL_FATTN_VEC_TURBO(type_K, type_V) \
    extern DECL_FATTN_VEC_CASE( 64, type_K, type_V); \
    extern DECL_FATTN_VEC_CASE(128, type_K, type_V); \
    extern DECL_FATTN_VEC_CASE(256, type_K, type_V); \
    extern DECL_FATTN_VEC_CASE(512, type_K, type_V);

#define EXTERN_DECL_FATTN_VEC_TURBO_NO_SOFTCAP(type_K, type_V) \
    extern DECL_FATTN_VEC_CASE_DECODE_NO_SOFTCAP( 64, type_K, type_V); \
    extern DECL_FATTN_VEC_CASE_PREFILL_NO_SOFTCAP( 64, type_K, type_V); \
    extern DECL_FATTN_VEC_CASE_DECODE_NO_SOFTCAP(128, type_K, type_V); \
    extern DECL_FATTN_VEC_CASE_PREFILL_NO_SOFTCAP(128, type_K, type_V); \
    extern DECL_FATTN_VEC_CASE_DECODE_NO_SOFTCAP(256, type_K, type_V); \
    extern DECL_FATTN_VEC_CASE_PREFILL_NO_SOFTCAP(256, type_K, type_V); \
    extern DECL_FATTN_VEC_CASE_DECODE_NO_SOFTCAP(512, type_K, type_V); \
    extern DECL_FATTN_VEC_CASE_PREFILL_NO_SOFTCAP(512, type_K, type_V);

#define EXTERN_DECL_FATTN_VEC_TURBO_Q8_NO_SOFTCAP(type_K, type_V) \
    extern DECL_FATTN_VEC_CASE_DECODE_NO_SOFTCAP( 64, type_K, type_V); \
    extern DECL_FATTN_VEC_CASE_PREFILL_NO_SOFTCAP( 64, type_K, type_V); \
    extern DECL_FATTN_VEC_CASE_DECODE_NO_SOFTCAP(128, type_K, type_V); \
    extern DECL_FATTN_VEC_CASE_PREFILL_NO_SOFTCAP(128, type_K, type_V); \
    extern DECL_FATTN_VEC_CASE_DECODE_NO_SOFTCAP(256, type_K, type_V); \
    extern DECL_FATTN_VEC_CASE_PREFILL_NO_SOFTCAP(256, type_K, type_V);

// Symmetric turbo types
EXTERN_DECL_FATTN_VEC_TURBO_NO_SOFTCAP(GGML_TYPE_TURBO3_0, GGML_TYPE_TURBO3_0)
EXTERN_DECL_FATTN_VEC_TURBO(GGML_TYPE_TURBO4_0, GGML_TYPE_TURBO4_0)
EXTERN_DECL_FATTN_VEC_TURBO_NO_SOFTCAP(GGML_TYPE_TURBO2_0, GGML_TYPE_TURBO2_0)
EXTERN_DECL_FATTN_VEC_TURBO(GGML_TYPE_TURBO1_5, GGML_TYPE_TURBO1_5)

// turbo × q8_0 cross-types
EXTERN_DECL_FATTN_VEC_TURBO_Q8_NO_SOFTCAP(GGML_TYPE_TURBO3_0, GGML_TYPE_Q8_0)
EXTERN_DECL_FATTN_VEC_TURBO(GGML_TYPE_Q8_0,     GGML_TYPE_TURBO3_0)
EXTERN_DECL_FATTN_VEC_TURBO(GGML_TYPE_TURBO4_0, GGML_TYPE_Q8_0)
EXTERN_DECL_FATTN_VEC_TURBO(GGML_TYPE_Q8_0,     GGML_TYPE_TURBO4_0)
EXTERN_DECL_FATTN_VEC_TURBO_Q8_NO_SOFTCAP(GGML_TYPE_TURBO2_0, GGML_TYPE_Q8_0)
EXTERN_DECL_FATTN_VEC_TURBO(GGML_TYPE_Q8_0,     GGML_TYPE_TURBO2_0)
EXTERN_DECL_FATTN_VEC_TURBO(GGML_TYPE_TURBO1_5, GGML_TYPE_Q8_0)
EXTERN_DECL_FATTN_VEC_TURBO(GGML_TYPE_Q8_0,     GGML_TYPE_TURBO1_5)

// turbo × turbo cross-types (all permutations)
EXTERN_DECL_FATTN_VEC_TURBO(GGML_TYPE_TURBO4_0, GGML_TYPE_TURBO3_0)
EXTERN_DECL_FATTN_VEC_TURBO(GGML_TYPE_TURBO4_0, GGML_TYPE_TURBO2_0)
EXTERN_DECL_FATTN_VEC_TURBO(GGML_TYPE_TURBO4_0, GGML_TYPE_TURBO1_5)
EXTERN_DECL_FATTN_VEC_TURBO(GGML_TYPE_TURBO3_0, GGML_TYPE_TURBO4_0)
EXTERN_DECL_FATTN_VEC_TURBO(GGML_TYPE_TURBO3_0, GGML_TYPE_TURBO2_0)
EXTERN_DECL_FATTN_VEC_TURBO(GGML_TYPE_TURBO3_0, GGML_TYPE_TURBO1_5)
EXTERN_DECL_FATTN_VEC_TURBO(GGML_TYPE_TURBO2_0, GGML_TYPE_TURBO4_0)
EXTERN_DECL_FATTN_VEC_TURBO(GGML_TYPE_TURBO2_0, GGML_TYPE_TURBO3_0)
EXTERN_DECL_FATTN_VEC_TURBO(GGML_TYPE_TURBO2_0, GGML_TYPE_TURBO1_5)
EXTERN_DECL_FATTN_VEC_TURBO(GGML_TYPE_TURBO1_5, GGML_TYPE_TURBO4_0)
EXTERN_DECL_FATTN_VEC_TURBO(GGML_TYPE_TURBO1_5, GGML_TYPE_TURBO3_0)
EXTERN_DECL_FATTN_VEC_TURBO(GGML_TYPE_TURBO1_5, GGML_TYPE_TURBO2_0)

// turbo × f16 cross-types
EXTERN_DECL_FATTN_VEC_TURBO(GGML_TYPE_TURBO4_0, GGML_TYPE_F16)
EXTERN_DECL_FATTN_VEC_TURBO(GGML_TYPE_TURBO3_0, GGML_TYPE_F16)
EXTERN_DECL_FATTN_VEC_TURBO(GGML_TYPE_TURBO2_0, GGML_TYPE_F16)
EXTERN_DECL_FATTN_VEC_TURBO(GGML_TYPE_TURBO1_5, GGML_TYPE_F16)
EXTERN_DECL_FATTN_VEC_TURBO(GGML_TYPE_F16, GGML_TYPE_TURBO4_0)
EXTERN_DECL_FATTN_VEC_TURBO(GGML_TYPE_F16, GGML_TYPE_TURBO3_0)
EXTERN_DECL_FATTN_VEC_TURBO(GGML_TYPE_F16, GGML_TYPE_TURBO2_0)
EXTERN_DECL_FATTN_VEC_TURBO(GGML_TYPE_F16, GGML_TYPE_TURBO1_5)

// TCQ types (D=128, 256 only — D=64 excluded because QK block size is 128)
#define EXTERN_DECL_FATTN_VEC_TCQ(type_K, type_V) \
    extern DECL_FATTN_VEC_CASE(128, type_K, type_V); \
    extern DECL_FATTN_VEC_CASE(256, type_K, type_V);

#define EXTERN_DECL_FATTN_VEC_TCQ_NO_SOFTCAP(type_K, type_V) \
    extern DECL_FATTN_VEC_CASE_DECODE_NO_SOFTCAP(128, type_K, type_V); \
    extern DECL_FATTN_VEC_CASE_PREFILL_NO_SOFTCAP(128, type_K, type_V); \
    extern DECL_FATTN_VEC_CASE_DECODE_NO_SOFTCAP(256, type_K, type_V); \
    extern DECL_FATTN_VEC_CASE_PREFILL_NO_SOFTCAP(256, type_K, type_V);

#define EXTERN_DECL_FATTN_VEC_TCQ_DECODE_NO_SOFTCAP(type_K, type_V) \
    extern DECL_FATTN_VEC_CASE_DECODE_NO_SOFTCAP(256, type_K, type_V);

#define EXTERN_DECL_FATTN_VEC_TCQ_PREFILL_NO_SOFTCAP(type_K, type_V) \
    extern DECL_FATTN_VEC_CASE_PREFILL_NO_SOFTCAP(256, type_K, type_V);

// Symmetric TCQ types
EXTERN_DECL_FATTN_VEC_TCQ_NO_SOFTCAP(GGML_TYPE_TURBO3_TCQ, GGML_TYPE_TURBO3_TCQ)
EXTERN_DECL_FATTN_VEC_TCQ(GGML_TYPE_TURBO2_TCQ, GGML_TYPE_TURBO2_TCQ)

// TCQ cross-types (turbo3_tcq x turbo2_tcq)
EXTERN_DECL_FATTN_VEC_TCQ(GGML_TYPE_TURBO3_TCQ, GGML_TYPE_TURBO2_TCQ)
EXTERN_DECL_FATTN_VEC_TCQ(GGML_TYPE_TURBO2_TCQ, GGML_TYPE_TURBO3_TCQ)

// TCQ x q8_0 cross-types used by layer-adaptive TCQ.
EXTERN_DECL_FATTN_VEC_TCQ_DECODE_NO_SOFTCAP(GGML_TYPE_TURBO3_TCQ, GGML_TYPE_Q8_0)
EXTERN_DECL_FATTN_VEC_TCQ_PREFILL_NO_SOFTCAP(GGML_TYPE_TURBO3_TCQ, GGML_TYPE_Q8_0)
EXTERN_DECL_FATTN_VEC_TCQ_NO_SOFTCAP(GGML_TYPE_Q8_0, GGML_TYPE_TURBO3_TCQ)
